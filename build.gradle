plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
//    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

group 'io.github.over-run'
archivesBaseName = 'mc2d'
version '0.6.0'

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/central' }
}

project.ext.lwjglVersion = '3.2.3'

dependencies {
    // May use Gson
    // implementation 'com.google.code.gson:gson:2.8.6'
    // implementation 'com.google.guava:guava:30.1-jre'
    implementation 'it.unimi.dsi:fastutil:8.5.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.0'
    compileOnly 'org.jetbrains:annotations:20.1.0'
    // Lwjgl libraries
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    for (String depend in ['', '-glfw', '-opengl']) {
        implementation "org.lwjgl:lwjgl$depend"
        for (String platform in ['windows', 'windows-x86', 'linux-arm64', 'linux-arm32', 'linux', 'macos']) {
            runtimeOnly "org.lwjgl:lwjgl$depend::natives-$platform"
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from 'LICENSE'
    manifest {
        attributes 'Main-Class': 'io.github.overrun.mc2d.Main',
                'Specification-Title': 'lwjgl3',
                'Specification-Vendor': 'LWJGL',
                'Specification-Version': lwjglVersion,
                'Implementation-Title': 'Minecraft2D',
                'Implementation-Vendor': 'Over-Run Organization',
                'Implementation-Version': archiveVersion,
                'Multi-Release': true
    }
}

//shadowJar {
//    from 'LICENSE'
//}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
    from 'LICENSE'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from 'build/docs/javadoc'
    from 'LICENSE'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy jar
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

idea.module.inheritOutputDirs = true
